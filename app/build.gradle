def secrets = System.getenv("GOOGLE_SERVICES_JSON")
def analyticsEnabled = secrets != null

apply plugin: "com.android.application"

if (analyticsEnabled) {
    apply plugin: "com.google.gms.google-services"
    apply plugin: "com.google.firebase.crashlytics"
}

android {
    compileSdk = 36

    def gitCommitHash = "git rev-parse HEAD".execute().text.trim()
    def shortCommitHash = "git rev-parse --short HEAD".execute().text.trim()

    defaultConfig {
        applicationId = "pro.sketchware"
        namespace = "pro.sketchware"
        //noinspection ExpiredTargetSdkVersion since we don't target getting Sketchware Pro on Google Play.
        targetSdk = 28
        minSdk = 26
        versionCode = 150
        versionName = "v7.0.0-daydream-${shortCommitHash}"

        buildConfigField("String", "GIT_HASH", "\"${gitCommitHash}\"")

        buildConfigField("String", "SKETCHUB_API_KEY", "\"${System.getenv("SKETCHUB_API_KEY")}\"")
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles (getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
            signingConfig = signingConfigs.debug
        }
    }

    packagingOptions {
        resources.excludes += "license/*"
        resources.excludes += "META-INF/DEPENDENCIES"
        resources.pickFirsts += "api_database/*"
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    androidResources {
        additionalParameters.addAll("--stable-ids", "${rootDir}/public-stable-ids.txt")
    }

    signingConfigs {
        debug {
            storeFile = file("../testkey.keystore")
            storePassword = "testkey"
            keyAlias = "testkey"
            keyPassword = "testkey"
        }
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
        resValues = true
    }

    configurations.implementation {
        exclude group: 'javax.inject', module: 'javax.inject'
        exclude group: "org.jetbrains.kotlin", module: "kotlin-scripting-compiler-embeddable"
        exclude group: "org.jetbrains.kotlin", module: "kotlin-scripting-common"
        exclude group: "org.jetbrains.kotlin", module: "kotlin-scripting-jvm"
        exclude group: "org.jetbrains.kotlin", module: "kotlin-script-runtime"
    }

    flavorDimensions "default"
    productFlavors {
        android26 {
            versionName "v7.0.0-daydream-android26-${shortCommitHash}"
            dimension "default"
            resValue "integer", "for_min_sdk", "26"
            resValue "bool", "enable_relase_app", "true"
            resValue "bool", "enable_r8", "true"
        }
        android33 {
            versionName "v7.0.0-daydream-android33-${shortCommitHash}"
            dimension "default"
            resValue "integer", "for_min_sdk", "33"
            resValue "bool", "enable_relase_app", "true"
            resValue "bool", "enable_r8", "true"
        }
    }
}

androidComponents {
    onVariants(selector().all()) { variant ->

        def flavorName = variant.productFlavors.isEmpty()
                ? "noflavor"
                : variant.productFlavors.collect { it.second }.join("-")

        def buildType = variant.buildType

        variant.outputs.forEach { output ->
            output.outputFileName.set(
                    "app-${buildType}-${flavorName}.apk"
            )
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"])

    implementation "androidx.appcompat:appcompat:1.7.1"
    implementation "androidx.core:core-splashscreen:1.2.0"
    implementation "androidx.activity:activity-ktx:1.12.2"
    implementation "androidx.recyclerview:recyclerview:1.4.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.viewpager:viewpager:1.1.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.2.0"
    implementation "com.google.android.material:material:1.14.0-alpha08"
    implementation "com.airbnb.android:lottie:6.7.1"
    implementation "com.google.android.flexbox:flexbox:3.0.0"

    implementation "com.github.bumptech.glide:glide:5.0.5"

    implementation 'androidx.exifinterface:exifinterface:1.4.2'
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.activity:activity-ktx:1.12.2'

    def editorGroupId = "io.github.Rosemoe.sora-editor"
    implementation platform("$editorGroupId:bom:0.23.5")
    implementation "$editorGroupId:editor"
    implementation "$editorGroupId:language-java"
    implementation "$editorGroupId:language-textmate"

    implementation "de.hdodenhof:circleimageview:3.1.0"

    implementation 'io.coil-kt:coil:2.7.0'
    implementation 'io.coil-kt:coil-svg:2.7.0'

    android26Implementation "com.android.tools.build:bundletool:1.18.1"
    android33Implementation "com.android.tools.build:bundletool:1.18.3"

    //noinspection GradleDependency copied from above dependency for not just runtime access
    android26Implementation "com.google.protobuf:protobuf-java:3.19.2"
    android33Implementation "com.google.protobuf:protobuf-java:4.33.2"
    //noinspection GradleDependency
    android26Implementation("com.android.tools:sdklib:25.3.0") {
        exclude group: "com.intellij", module: "annotations"
    }
    android33Implementation("com.android.tools:sdklib:26.4.3") {
        exclude group: "com.intellij", module: "annotations"
    }
    android26Implementation "com.android.tools:r8:8.11.18"
    android33Implementation "com.android.tools:r8:8.13.17"
    implementation "com.github.Iyxan23:zipalign-java:1.2.2"

    implementation "com.google.code.gson:gson:2.13.2"
    implementation "com.madgag:scpkix-jdk15on:1.47.0.3"

    implementation 'io.noties.markwon:core:4.6.2'

    implementation "com.squareup.okhttp3:okhttp:5.3.2"
    implementation "com.squareup.okio:okio:3.16.4"

    android26Implementation "org.eclipse.jdt:ecj:3.26.0"
    android33Implementation "org.eclipse.jdt:ecj:3.34.0"

    implementation "com.github.waelchateur:kotlinc-for-sketchware:2.1.21_rc3"
    compileOnly "org.jetbrains.kotlin:kotlin-compiler:2.1.21"
    implementation 'io.github.itsaky:nb-javac-android:17.0.0.3'
    implementation 'com.fasterxml.woodstox:woodstox-core:6.5.1'
    implementation 'javax.xml.stream:stax-api:1.0-2'

    implementation "com.github.Cosmic-Ide:DependencyResolver:05ec26af0b"
    implementation "com.github.PranavPurwar:filepicker:74526bea66"

    implementation 'com.guardsquare:proguard-core:9.0.2'

    implementation "com.github.topjohnwu.libsu:core:5.0.4"
    implementation "dev.chrisbanes.insetter:insetter:0.6.1"

    android26Implementation("com.github.javaparser:javaparser-symbol-solver-core:3.25.3") {
        exclude group: "com.google.guava", module: "guava"
    }
    android33Implementation("com.github.javaparser:javaparser-symbol-solver-core:3.27.1") {
        exclude group: "com.google.guava", module: "guava"
    }

    // Analytics and Crashlytics
    if (analyticsEnabled) {
        new File("$projectDir/google-services.json").text = secrets
    }
    implementation platform("com.google.firebase:firebase-bom:34.7.0")
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-messaging"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs_nio:2.1.5"

    implementation "net.lingala.zip4j:zip4j:2.11.5"

    implementation "org.eclipse.jgit:org.eclipse.jgit:7.3.0.202506031305-r"
}